# Maintainer: Gerardo Exequiel Pozzi <djgera@archlinux.org>
# Maintainer: Pierre Schmitz <pierre@archlinux.de>

# Customized version for SystemRescue https://www.system-rescue.org/
# see https://gitlab.com/systemrescue/systemrescue-sources/-/tree/main/patches

pkgname=archiso
pkgver=43
pkgrel=17
pkgdesc='Tools for creating Arch Linux live and install iso images, customized for SystemRescue'
arch=('any')
license=('GPL')
url='https://gitlab.archlinux.org/archlinux/archiso'
depends=('make' 'arch-install-scripts' 'squashfs-tools' 'libisoburn' 'dosfstools' 'lynx' 'grub')
source=("https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz"
        "https://sources.archlinux.org/other/${pkgname}/${pkgname}-${pkgver}.tar.gz.sig"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-01-squashfs-options.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-02-fix-pxeboot-dhcp.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-03-fix-pxeboot-multiple-interfaces.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-04-increase-cow-spacesize.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-05-add-loadsrm.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-06-copytoram.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-07-yaml-config.patch"
        "https://gitlab.com/systemrescue/systemrescue-sources/-/raw/c42634e320a3c60c4c4bc1299bcc593a5a115584/patches/archiso-v43-08-glibc-fix-dns.patch"
        )
sha256sums=('4d2274cc5f1c48883941c3e37dfe7882632f9c455f97635aa079c1812571cb13'
            'SKIP'
            'b0a6f5dbb16f04da7cf9b98087e4a81ced97d874a1d7f18334e6fca32d07120c'
            'b84eb0c9ce356223995f88222cf90fc975e1089d88cc0649275bb629a0a94de2'
            'dc67a7414cc26736405b7ab1f3a1c743a9f47251637a47b1744be6ebd5cbcf11'
            '7019657c12a9104d05e969ae6c74eba4554c6404d37b6096b5fcef87eb895688'
            'd2c3ffb9bb86199de1b03fdc215d3f101dd23b9c570c45acd62796ba0ee14c2a'
            '37dea83a9c26eef423177b52422c982d597713231f82d86ad910ea5e9537959f'
            'c1e0bd5a52f231d963736a4f6996f6088cf39004bcabdd981a874006b8cc4491'
            '6d479cb3ac48fd16f94ade229b13c79d2d3ab7aa0d28cafc4750b323486b3dc3')
validpgpkeys=('0F334D8698881578F65D2AE55ED514A45BD5C938'  # Gerardo Exequiel Pozzi
              '4AA4767BBC9C4B1D18AE28B77F2D434B9741E8AC') # Pierre Schmitz

prepare() {
    cd $pkgname-$pkgver
    patch -p1 < ../archiso-v43-01-squashfs-options.patch
    patch -p1 < ../archiso-v43-02-fix-pxeboot-dhcp.patch
    patch -p1 < ../archiso-v43-03-fix-pxeboot-multiple-interfaces.patch
    patch -p1 < ../archiso-v43-04-increase-cow-spacesize.patch
    patch -p1 < ../archiso-v43-05-add-loadsrm.patch
    patch -p1 < ../archiso-v43-06-copytoram.patch
    patch -p1 < ../archiso-v43-07-yaml-config.patch
    patch -p1 < ../archiso-v43-08-glibc-fix-dns.patch
}

package() {
    make -C "${pkgname}-${pkgver}" DESTDIR="${pkgdir}" install
}
