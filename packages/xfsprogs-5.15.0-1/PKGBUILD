# Maintainer : Tobias Powalowski <tpowa@archlinux.org>
pkgname=xfsprogs
pkgver=5.15.0
pkgrel=1
pkgdesc="XFS filesystem utilities"
arch=('x86_64' 'i686')
license=('LGPL')
url="https://xfs.org"
depends=('sh' 'util-linux-libs' 'icu' 'device-mapper' 'libinih' 'liburcu')
optdepends=('python: for xfs_scrub_all script'
            'smtp-forwarder: for xfs_scrub_fail script')
makedepends=('git')
options=('!makeflags')
source=("https://mirrors.edge.kernel.org/pub/linux/utils/fs/xfs/xfsprogs/xfsprogs-${pkgver}.tar.xz")
sha256sums=('13d2bf3830eba0027ab558825439dd521f61c7266783d7917213432872e74d56')

build() {
  cd xfsprogs-${pkgver}
  make configure
  export OPTIMIZER="-march=${CARCH/_/-} -O1"
  export DEBUG=-DNDEBUG
  ./configure --prefix=/usr --sbindir=/usr/bin --with-systemd-unit-dir=/usr/lib/systemd/system
  make
}

package() {
  cd xfsprogs-${pkgver}
  make DIST_ROOT="${pkgdir}" PKG_ROOT_SBIN_DIR="/usr/bin" install install-dev
  chown -R root "$pkgdir"
  chgrp -R root "$pkgdir"
  # add hack as we cannot set rootlibdir
  mv "${pkgdir}"/lib/libhandle.so* "${pkgdir}/usr/lib/"
  rm -rf "${pkgdir}/lib"
}
