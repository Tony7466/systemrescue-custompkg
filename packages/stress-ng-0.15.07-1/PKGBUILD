# Maintainer: Iyán Méndez Veiga <me (at) iyanmv (dot) com>
# Contributor: Thomas Wucher <arch (at) thomaswucher (dot) de>
pkgname=stress-ng
pkgver=0.15.07
pkgrel=1
pkgdesc="Software to stress test a computer system in various selectable ways"
arch=('x86_64')
url="https://github.com/ColinIanKing/stress-ng"
license=('GPL')

# reduce dependencies for SystemRescue (in comparison to regular AUR package):
# this saves much disk space by not including things like full wayland
# build detects the installed libraries and does not include the missing ones

depends=(
    'judy'
    'kmod'
    'libbsd'
    'lksctp-tools'
    'xxhash'
)
makedepends=(
    'attr'
    'keyutils'
    'libaio'
    'libcap'
    'libgcrypt'
    'libmd'
)
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ColinIanKing/${pkgname}/archive/refs/tags/V${pkgver}.tar.gz"
            https://github.com/ColinIanKing/stress-ng/commit/710d676175ae4a7461d136f7a322cd7cdc69ad41.patch )
b2sums=('15e4aec04313a1c286c080a9a4d646a7908e213fe8680fd105017a23843f094185ee2564b497d8d3103389b6b9d6a512ec150a47202a59c4a4dad554425f655b'
        'bfa422cf6a026ede19a8246f53d156476524810627ce72cfb0c75ddd526628da24ca2b2c735d57ecfed81488d22a23bd6717698e8a108d3fa902a9ac12c4e023')

build() {
    cd "${pkgname}-${pkgver}"
    
    # add fused-multiple-add stressor from upstream, used to max out cpu power consumption
    patch -p 1 <../710d676175ae4a7461d136f7a322cd7cdc69ad41.patch
    make clean && make
}

package() {
    cd "${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}/" install
}
